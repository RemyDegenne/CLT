/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Distributions.Gaussian
import Mathlib.Analysis.SpecialFunctions.Gaussian.FourierTransform
import Clt.CharFun
import Clt.MomentGenerating

/-!
# Characteristic function in Banach spaces
-/

open Real Complex NormedSpace
open scoped ENNReal NNReal

lemma IsBoundedBilinearMap.symm {E F G ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
    [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]
    [SeminormedAddCommGroup G] [NormedSpace ğ•œ G]
    {f : E Ã— F â†’ G} (h : IsBoundedBilinearMap ğ•œ f) :
    IsBoundedBilinearMap ğ•œ (fun p â†¦ f (p.2, p.1)) where
  add_left xâ‚ xâ‚‚ y := h.add_right _ _ _
  smul_left c x y := h.smul_right _ _ _
  add_right x yâ‚ yâ‚‚ := h.add_left _ _ _
  smul_right c x y := h.smul_left _ _ _
  bound := by
    obtain âŸ¨C, hC_pos, hCâŸ© := h.bound
    exact âŸ¨C, hC_pos, fun x y â†¦ (hC y x).trans_eq (by ring)âŸ©

lemma ContinuousLinearMap.comp_inl_add_comp_inr
    {E F : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E]
    [SeminormedAddCommGroup F] [NormedSpace â„ F]
    (L : E Ã— F â†’L[â„] â„) (v : E Ã— F) :
    L.comp (.inl â„ E F) v.1 + L.comp (.inr â„ E F) v.2 = L v := by
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.comp_apply,
    ContinuousLinearMap.inl_apply, ContinuousLinearMap.inr_apply, â† ContinuousLinearMap.map_add]
  simp

namespace BoundedContinuousFunction

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E]

/-- The bounded continuous function `x â†¦ exp (L x * I)`, for a continuous linear form `L`. -/
noncomputable
def probCharCLM (L : E â†’L[â„] â„) : E â†’áµ‡ â„‚ :=
  char continuous_probChar (L := isBoundedBilinearMap_apply.symm.toContinuousLinearMap.toLinearMapâ‚‚)
    isBoundedBilinearMap_apply.symm.continuous L

lemma probCharCLM_apply (L : E â†’L[â„] â„) (x : E) : probCharCLM L x = exp (L x * I) := rfl

@[simp]
lemma probCharCLM_zero : probCharCLM (0 : E â†’L[â„] â„) = 1 := by simp [probCharCLM]

end BoundedContinuousFunction

namespace MeasureTheory

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {mE : MeasurableSpace E}
  [NormedAddCommGroup F] [NormedSpace â„ F] {mF : MeasurableSpace F}
  {Î¼ : Measure E} {Î½ : Measure F}

/-- The characteristic function of a measure in a normed space, function from `E â†’L[â„] â„` to `â„‚`
with `charFunCLM Î¼ L = âˆ« v, exp (L v * I) âˆ‚Î¼`. -/
noncomputable
def charFunCLM (Î¼ : Measure E) (L : E â†’L[â„] â„) : â„‚ :=
  âˆ« v, BoundedContinuousFunction.probCharCLM L v âˆ‚Î¼

lemma charFunCLM_apply (L : E â†’L[â„] â„) : charFunCLM Î¼ L = âˆ« v, exp (L v * I) âˆ‚Î¼ := rfl

lemma charFunCLM_prod [SFinite Î¼] [SFinite Î½] (L : E Ã— F â†’L[â„] â„) :
    charFunCLM (Î¼.prod Î½) L
      = charFunCLM Î¼ (L.comp (.inl â„ E F)) * charFunCLM Î½ (L.comp (.inr â„ E F)) := by
  let Lâ‚ : E â†’L[â„] â„ := L.comp (.inl â„ E F)
  let Lâ‚‚ : F â†’L[â„] â„ := L.comp (.inr â„ E F)
  simp_rw [charFunCLM_apply, â† L.comp_inl_add_comp_inr, ofReal_add, add_mul,
    Complex.exp_add]
  rw [integral_prod_mul (f := fun x â†¦ cexp ((Lâ‚ x * I))) (g := fun x â†¦ cexp ((Lâ‚‚ x * I)))]

lemma charFunCLM_eq_charFun_map_one [BorelSpace E] (L : E â†’L[â„] â„) :
    charFunCLM Î¼ L = charFun (Î¼.map L) 1 := by
  rw [charFunCLM_apply]
  have : âˆ« x, cexp (L x * I) âˆ‚Î¼ = âˆ« x, cexp (x * I) âˆ‚(Î¼.map L) := by
    rw [integral_map]
    Â· fun_prop
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
  rw [this, charFun_apply]
  simp

lemma charFun_map_eq_charFunCLM_smul [BorelSpace E] (L : E â†’L[â„] â„) (u : â„) :
    charFun (Î¼.map L) u = charFunCLM Î¼ (u â€¢ L) := by
  rw [charFunCLM_apply]
  have : âˆ« x, cexp ((u â€¢ L) x * I) âˆ‚Î¼ = âˆ« x, cexp (u * x * I) âˆ‚(Î¼.map L) := by
    rw [integral_map]
    Â· simp
    Â· fun_prop
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
  rw [this, charFun_apply]
  simp

lemma charFunCLM_map [BorelSpace E] [BorelSpace F] {Î¼ : Measure E}
    (L : E â†’L[â„] F) (L' : F â†’L[â„] â„) :
    charFunCLM (Î¼.map L) L' = charFunCLM Î¼ (L'.comp L) := by
  rw [charFunCLM_eq_charFun_map_one, charFunCLM_eq_charFun_map_one,
    Measure.map_map (by fun_prop) (by fun_prop)]
  simp

variable [CompleteSpace E] [BorelSpace E] [SecondCountableTopology E]

/-- If two finite measures have the same characteristic function, then they are equal. -/
theorem ext_of_charFunCLM {Î¼ Î½ : Measure E} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (h : charFunCLM Î¼ = charFunCLM Î½) :
    Î¼ = Î½ := by
  refine ext_of_integral_char_eq continuous_probChar probChar_ne_one
    ?_ ?_ (fun L â†¦ funext_iff.mp h L)
  Â· intro v hv
    rw [ne_eq, LinearMap.ext_iff]
    simp only [ContinuousLinearMap.toLinearMapâ‚‚_apply, LinearMap.zero_apply, not_forall]
    change âˆƒ L : E â†’L[â„] â„, L v â‰  0
    by_contra! h
    exact hv (eq_zero_of_forall_dual_eq_zero _ h)
  Â· exact isBoundedBilinearMap_apply.symm.continuous

end MeasureTheory
