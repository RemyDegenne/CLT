/-
Copyright (c) 2024 Thomas Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Zhu, RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import Mathlib.MeasureTheory.Measure.FiniteMeasureExt
import Clt.Tight
import Clt.MomentGenerating
import Clt.Prokhorov

/-!
Inverting the characteristic function
-/

noncomputable section

open Filter MeasureTheory ProbabilityTheory BoundedContinuousFunction Real RCLike
open scoped Topology

section FromMathlibPR19761

-- See Mathlib#19761, these conditions might change
variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V]
    [MeasurableSpace V] [BorelSpace V] [CompleteSpace V] [SecondCountableTopology V]

/-- This is already proven in Mathlib#19761, for FiniteMeasure -/
theorem MeasureTheory.ProbabilityMeasure.ext_of_charFun_eq (Î¼ Î½ : ProbabilityMeasure V)
    (h : charFun (Î¼ : Measure V) = charFun Î½) :
    Î¼ = Î½ := by
  rw [funext_iff] at h
  simp_rw [charFun_eq_integral_char] at h
  suffices (Î¼ : Measure V) = Î½ by ext; rw [this]
  refine ext_of_integral_char_eq continuous_probChar probChar_ne_one ?_ ?_ h
  Â· exact fun v hv â†¦ DFunLike.ne_iff.mpr âŸ¨v, inner_self_ne_zero.mpr hvâŸ©
  Â· exact continuous_inner

end FromMathlibPR19761

lemma RCLike.lipschitzWith_re {ğ•œ : Type*} [RCLike ğ•œ] :
    LipschitzWith 1 (re (K := ğ•œ)) := by
  intro x y
  simp only [ENNReal.coe_one, one_mul, edist_eq_enorm_sub]
  calc â€–re x - re yâ€–â‚‘
  _ = â€–re (x - y)â€–â‚‘ := by rw [ AddMonoidHom.map_sub re x y]
  _ â‰¤ â€–x - yâ€–â‚‘ := by rw [enorm_le_iff_norm_le]; exact norm_re_le_norm (x - y)

lemma RCLike.lipschitzWith_im {ğ•œ : Type*} [RCLike ğ•œ] :
    LipschitzWith 1 (im (K := ğ•œ)) := by
  intro x y
  simp only [ENNReal.coe_one, one_mul, edist_eq_enorm_sub]
  calc â€–im x - im yâ€–â‚‘
  _ = â€–im (x - y)â€–â‚‘ := by rw [ AddMonoidHom.map_sub im x y]
  _ â‰¤ â€–x - yâ€–â‚‘ := by rw [enorm_le_iff_norm_le]; exact norm_im_le_norm (x - y)

lemma RCLike.isUniformEmbedding_ofReal {ğ•œ : Type*} [RCLike ğ•œ] :
    IsUniformEmbedding ((â†‘) : â„ â†’ ğ•œ) :=
  ofRealLI.isometry.isUniformEmbedding

lemma _root_.Filter.tendsto_ofReal_iff' {Î± ğ•œ : Type*} [RCLike ğ•œ]
    {l : Filter Î±} {f : Î± â†’ â„} {x : â„} :
    Tendsto (fun x â†¦ (f x : ğ•œ)) l (ğ“ (x : ğ•œ)) â†” Tendsto f l (ğ“ x) :=
  RCLike.isUniformEmbedding_ofReal.isClosedEmbedding.tendsto_nhds_iff.symm

theorem MeasureTheory.ProbabilityMeasure.tendsto_iff_forall_integral_rcLike_tendsto
    {Î³ Î© : Type*} (ğ•œ : Type*) [RCLike ğ•œ]
    {F : Filter Î³} {mÎ© : MeasurableSpace Î©} [TopologicalSpace Î©] [OpensMeasurableSpace Î©]
    {Î¼s : Î³ â†’ ProbabilityMeasure Î©} {Î¼ : ProbabilityMeasure Î©} :
    Tendsto Î¼s F (ğ“ Î¼) â†”
      âˆ€ f : Î© â†’áµ‡ ğ•œ,
        Tendsto (fun i â†¦ âˆ« Ï‰, f Ï‰ âˆ‚(Î¼s i : Measure Î©)) F (ğ“ (âˆ« Ï‰, f Ï‰ âˆ‚(Î¼ : Measure Î©))) := by
  rw [ProbabilityMeasure.tendsto_iff_forall_integral_tendsto]
  refine âŸ¨fun h f â†¦ ?_, fun h f â†¦ ?_âŸ©
  Â· rw [â† integral_re_add_im (integrable Î¼ f)]
    simp_rw [â† integral_re_add_im (integrable (Î¼s _) f)]
    refine Tendsto.add ?_ ?_
    Â· exact Tendsto.comp (continuous_ofReal.tendsto _) (h (f.comp re RCLike.lipschitzWith_re))
    Â· exact (Tendsto.comp (continuous_ofReal.tendsto _)
        (h (f.comp im RCLike.lipschitzWith_im))).mul_const _
  Â· specialize h ((ofRealAm (K := ğ•œ)).compLeftContinuousBounded â„ lipschitzWith_ofReal f)
    simp only [AlgHom.compLeftContinuousBounded_apply_apply, ofRealAm_coe,
      Complex.coe_algebraMap, integral_ofReal] at h
    exact tendsto_ofReal_iff'.mp h

lemma MeasureTheory.ProbabilityMeasure.tendsto_of_tight_of_separatesPoints
    {E ğ•œ : Type*} [RCLike ğ•œ] [MeasurableSpace E]
    [MetricSpace E] [CompleteSpace E] [SecondCountableTopology E] [BorelSpace E]
    {Î¼ : â„• â†’ ProbabilityMeasure E}
    (h_tight : IsTightMeasureSet {(Î¼ n : Measure E) | n}) {Î¼â‚€ : ProbabilityMeasure E}
    {A : StarSubalgebra ğ•œ (E â†’áµ‡ ğ•œ)} (hA : (A.map (toContinuousMapStarâ‚ ğ•œ)).SeparatesPoints)
    (heq : âˆ€ g âˆˆ A, Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(Î¼ n)) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼â‚€))) :
    Tendsto Î¼ atTop (ğ“ Î¼â‚€) := by
  refine Filter.tendsto_of_subseq_tendsto fun ns hns â†¦ ?_
  have h_compact : IsCompact (closure {Î¼ n | n}) :=
    isCompact_closure_of_isTightMeasureSet (S := {Î¼ n | n}) ?_
  swap; Â· convert h_tight; simp
  obtain âŸ¨Î¼', hÎ¼'_mem, Ï†, hÏ†_mono, hÏ†_tendstoâŸ© : âˆƒ Î¼' âˆˆ closure {Î¼ n | n},
      âˆƒ Ï†, StrictMono Ï† âˆ§ Tendsto ((fun n â†¦ Î¼ (ns n)) âˆ˜ Ï†) atTop (ğ“ Î¼') :=
    IsCompact.tendsto_subseq h_compact (x := fun n â†¦ Î¼ (ns n)) fun n â†¦ subset_closure âŸ¨ns n, rflâŸ©
  refine âŸ¨Ï†, ?_âŸ©
  suffices Î¼' = Î¼â‚€ from this â–¸ hÏ†_tendsto
  suffices (Î¼' : Measure E) = Î¼â‚€ by ext; rw [this]
  refine ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable hA
    fun g hg â†¦ ?_
  specialize heq g hg
  suffices Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(Î¼ (ns (Ï† n)))) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼')) from
    tendsto_nhds_unique this <| heq.comp (hns.comp hÏ†_mono.tendsto_atTop)
  rw [ProbabilityMeasure.tendsto_iff_forall_integral_rcLike_tendsto ğ•œ] at hÏ†_tendsto
  exact hÏ†_tendsto g

lemma MeasureTheory.ProbabilityMeasure.tendsto_charPoly_of_tendsto_charFun
    {Î¼ : â„• â†’ ProbabilityMeasure â„} {Î¼â‚€ : ProbabilityMeasure â„}
    (h : âˆ€ t : â„, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (charFun Î¼â‚€ t)))
    {g : â„ â†’áµ‡ â„‚}
    (hg : g âˆˆ charPoly continuous_probChar (L := bilinFormOfRealInner) continuous_inner) :
    Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(Î¼ n)) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼â‚€)) := by
  rw [mem_charPoly] at hg
  obtain âŸ¨w, hwâŸ© := hg
  have h_eq (Î¼ : Measure â„) (hÎ¼ : IsProbabilityMeasure Î¼) :
      âˆ« x, g x âˆ‚Î¼ = âˆ‘ a âˆˆ w.support, w a * âˆ« x, (probChar (bilinFormOfRealInner x a) : â„‚) âˆ‚Î¼ := by
    simp_rw [hw]
    rw [integral_finset_sum]
    Â· congr with y
      rw [integral_mul_left]
    Â· intro i hi
      refine Integrable.const_mul ?_ _
      fun_prop
  simp_rw [h_eq (Î¼ _), h_eq Î¼â‚€]
  refine tendsto_finset_sum _ fun y hy â†¦ Tendsto.const_mul _ ?_
  convert h y <;> {
      simp only [bilinFormOfRealInner_apply_apply, inner_apply, conj_trivial, probChar_apply,
        Complex.ofReal_mul, charFun_apply]
      congr with x
      rw [mul_comm (y : â„‚)] }

lemma MeasureTheory.ProbabilityMeasure.tendsto_of_tendsto_charFun {Î¼ : â„• â†’ ProbabilityMeasure â„}
    {Î¼â‚€ : ProbabilityMeasure â„}
    (h : âˆ€ t : â„, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (charFun Î¼â‚€ t))) :
    Tendsto Î¼ atTop (ğ“ Î¼â‚€) := by
  have h_tight : IsTightMeasureSet (ğ“§ := â„) {Î¼ n | n} :=
    isTightMeasureSet_of_tendsto_charFun (by fun_prop) (by fun_prop) h
  refine tendsto_of_tight_of_separatesPoints h_tight (ğ•œ := â„‚)
    (A := charPoly continuous_probChar (L := bilinFormOfRealInner) continuous_inner) ?_ ?_
  Â· refine separatesPoints_charPoly continuous_probChar probChar_ne_one _ ?_
    exact fun v hv â†¦ DFunLike.ne_iff.mpr âŸ¨v, inner_self_ne_zero.mpr hvâŸ©
  Â· exact fun g â†¦ tendsto_charPoly_of_tendsto_charFun h

/--
The LÃ©vy continuity theorem https://en.wikipedia.org/wiki/L%C3%A9vy%27s_continuity_theorem.

See blueprint.

The <= direction follows from definition, but it is not needed.
The => direction is much harder:
* If `Î¼s` is tight, then the statement follows in general
  * For each subsequence of `Î¼s`, we need find a sub-subsequence that converges weakly to `Î¼`.
    This requires Prokhorov's theorem for relative compactness.
* Î¼s is tight in `â„^d` if their `charFun`s are equicontinuous at 0
* This is in particular if they converge to a function continuous at 0

This is stated in â„, instead of `â„^d` as in the blueprint (TODO).
-/
theorem MeasureTheory.ProbabilityMeasure.tendsto_iff_tendsto_charFun {Î¼ : â„• â†’ ProbabilityMeasure â„}
    {Î¼â‚€ : ProbabilityMeasure â„} :
    Tendsto Î¼ atTop (ğ“ Î¼â‚€) â†”
      âˆ€ t : â„, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (charFun Î¼â‚€ t)) := by
  refine âŸ¨fun h t â†¦ ?_, tendsto_of_tendsto_charFunâŸ©
  rw [ProbabilityMeasure.tendsto_iff_forall_integral_rcLike_tendsto â„‚] at h
  simp_rw [charFun_apply_real]
  -- we need `(x : â„) â†¦ Complex.exp (x * t * I)` as a `â„ â†’áµ‡ â„‚` to apply `h`
  let expb : â„ â†’áµ‡ â„‚ :=
  { toFun := fun x â†¦ Complex.exp (x * t * I),
    continuous_toFun := by fun_prop
    map_bounded' := by
      refine âŸ¨2, fun x y â†¦ ?_âŸ©
      simp only [I_to_complex]
      calc dist _ _
          â‰¤ (â€–_â€– : â„) + â€–_â€– := dist_le_norm_add_norm _ _
        _ â‰¤ 1 + 1 := add_le_add (by norm_cast; rw [Complex.norm_exp_ofReal_mul_I])
            (by norm_cast; rw [Complex.norm_exp_ofReal_mul_I])
        _ = 2 := by ring }
  exact h expb
