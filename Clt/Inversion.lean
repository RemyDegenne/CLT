/-
Copyright (c) 2024 Thomas Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Zhu, RÃ©my Degenne, Miyahara KÅ
-/
import Clt.MomentGenerating
import Clt.Prokhorov
import Clt.Tight
import Mathlib.MeasureTheory.Measure.LevyProkhorovMetric

/-!
Inverting the characteristic function
-/

noncomputable section

open Function Filter MeasureTheory ProbabilityMeasure ProbabilityTheory BoundedContinuousFunction
  Real RCLike
open scoped Topology RealInnerProductSpace

section ForMathlib

lemma MeasureTheory.charFun_map_inner_right
    {E : Type*} {mE : MeasurableSpace E} {Î¼ : Measure E}
    [SeminormedAddCommGroup E] [InnerProductSpace â„ E] [OpensMeasurableSpace E]
    (r : â„) (t : E) :
    charFun (Î¼.map (fun (x : E) => âŸªx, tâŸ«)) r = charFun Î¼ (r â€¢ t) := by
  rw [charFun_apply_real, charFun_apply, integral_map (by fun_prop) (by fun_prop)]
  simp [real_inner_smul_right]

end ForMathlib

variable (ğ•œ : Type*) [RCLike ğ•œ]

lemma MeasureTheory.ProbabilityMeasure.tendsto_of_tight_of_separatesPoints
    {E : Type*} [MeasurableSpace E]
    [MetricSpace E] [CompleteSpace E] [SecondCountableTopology E] [BorelSpace E]
    {Î¼ : â„• â†’ ProbabilityMeasure E}
    (h_tight : IsTightMeasureSet {(Î¼ n : Measure E) | n}) {Î¼â‚€ : ProbabilityMeasure E}
    {A : StarSubalgebra ğ•œ (E â†’áµ‡ ğ•œ)} (hA : (A.map (toContinuousMapStarâ‚ ğ•œ)).SeparatesPoints)
    (heq : âˆ€ g âˆˆ A, Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(Î¼ n)) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼â‚€))) :
    Tendsto Î¼ atTop (ğ“ Î¼â‚€) := by
  refine Filter.tendsto_of_subseq_tendsto fun ns hns â†¦ ?_
  have h_compact : IsCompact (closure {Î¼ n | n}) :=
    isCompact_closure_of_isTightMeasureSet (S := {Î¼ n | n}) ?_
  swap; Â· convert h_tight; simp
  obtain âŸ¨Î¼', hÎ¼'_mem, Ï†, hÏ†_mono, hÏ†_tendstoâŸ© : âˆƒ Î¼' âˆˆ closure {Î¼ n | n},
      âˆƒ Ï†, StrictMono Ï† âˆ§ Tendsto ((fun n â†¦ Î¼ (ns n)) âˆ˜ Ï†) atTop (ğ“ Î¼') :=
    IsCompact.tendsto_subseq h_compact (x := fun n â†¦ Î¼ (ns n)) fun n â†¦ subset_closure âŸ¨ns n, rflâŸ©
  refine âŸ¨Ï†, ?_âŸ©
  suffices Î¼' = Î¼â‚€ from this â–¸ hÏ†_tendsto
  suffices (Î¼' : Measure E) = Î¼â‚€ by ext; rw [this]
  refine ext_of_forall_mem_subalgebra_integral_eq_of_pseudoEMetric_complete_countable hA
    fun g hg â†¦ ?_
  specialize heq g hg
  suffices Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(Î¼ (ns (Ï† n)))) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼')) from
    tendsto_nhds_unique this <| heq.comp (hns.comp hÏ†_mono.tendsto_atTop)
  rw [tendsto_iff_forall_integral_rclike_tendsto ğ•œ] at hÏ†_tendsto
  exact hÏ†_tendsto g

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
  [MeasurableSpace E] [BorelSpace E]
  {Î¼ : â„• â†’ ProbabilityMeasure E} {Î¼â‚€ : ProbabilityMeasure E}

lemma MeasureTheory.ProbabilityMeasure.tendsto_charPoly_of_tendsto_charFun
    (h : âˆ€ t : E, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (charFun Î¼â‚€ t)))
    {g : E â†’áµ‡ â„‚}
    (hg : g âˆˆ charPoly continuous_probChar (L := innerâ‚— E) continuous_inner) :
    Tendsto (fun n â†¦ âˆ« x, g x âˆ‚(Î¼ n)) atTop (ğ“ (âˆ« x, g x âˆ‚Î¼â‚€)) := by
  rw [mem_charPoly] at hg
  obtain âŸ¨w, hwâŸ© := hg
  have h_eq (Î¼ : Measure E) (hÎ¼ : IsProbabilityMeasure Î¼) :
      âˆ« x, g x âˆ‚Î¼ = âˆ‘ a âˆˆ w.support, w a * âˆ« x, (probChar (innerâ‚— E x a) : â„‚) âˆ‚Î¼ := by
    simp_rw [hw]
    rw [integral_finset_sum]
    Â· congr with y
      rw [integral_const_mul]
    Â· intro i hi
      refine Integrable.const_mul ?_ _
      change Integrable (fun x â†¦ innerProbChar i x) Î¼
      exact BoundedContinuousFunction.integrable Î¼ _
  simp_rw [h_eq (Î¼ _), h_eq Î¼â‚€]
  refine tendsto_finset_sum _ fun y hy â†¦ Tendsto.const_mul _ ?_
  simp only [innerâ‚—_apply_apply]
  simp_rw [â† charFun_eq_integral_probChar]
  exact h y

lemma MeasureTheory.ProbabilityMeasure.tendsto_of_tendsto_charFun
    [FiniteDimensional â„ E]
    (h : âˆ€ t : E, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (charFun Î¼â‚€ t))) :
    Tendsto Î¼ atTop (ğ“ Î¼â‚€) := by
  have h_tight : IsTightMeasureSet (ğ“§ := E) {Î¼ n | n} :=
    isTightMeasureSet_of_tendsto_charFun (by fun_prop) (by fun_prop) h
  refine tendsto_of_tight_of_separatesPoints h_tight (ğ•œ := â„‚)
    (A := charPoly continuous_probChar (L := innerâ‚— E) continuous_inner) ?_ ?_
  Â· refine separatesPoints_charPoly continuous_probChar probChar_ne_one _ ?_
    exact fun v hv â†¦ DFunLike.ne_iff.mpr âŸ¨v, inner_self_ne_zero.mpr hvâŸ©
  Â· exact fun g â†¦ tendsto_charPoly_of_tendsto_charFun h

/--
The LÃ©vy continuity theorem https://en.wikipedia.org/wiki/L%C3%A9vy%27s_continuity_theorem.

See blueprint.

The <= direction follows from definition, but it is not needed.
The => direction is much harder:
* If `Î¼s` is tight, then the statement follows in general
  * For each subsequence of `Î¼s`, we need find a sub-subsequence that converges weakly to `Î¼`.
    This requires Prokhorov's theorem for relative compactness.
* Î¼s is tight in `â„^d` if their `charFun`s converge to a function continuous at 0

-/
theorem MeasureTheory.ProbabilityMeasure.tendsto_iff_tendsto_charFun
    [FiniteDimensional â„ E] :
    Tendsto Î¼ atTop (ğ“ Î¼â‚€) â†”
      âˆ€ t : E, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (charFun Î¼â‚€ t)) := by
  refine âŸ¨fun h t â†¦ ?_, tendsto_of_tendsto_charFunâŸ©
  rw [tendsto_iff_forall_integral_rclike_tendsto â„‚] at h
  simp_rw [charFun_eq_integral_innerProbChar]
  exact h (innerProbChar t)

lemma ProbabilityTheory.tendsto_iff_forall_tendto_inner_right
    {E : Type*} {mE : MeasurableSpace E}
    [NormedAddCommGroup E] [InnerProductSpace â„ E] [FiniteDimensional â„ E] [BorelSpace E]
    {P : â„• â†’ ProbabilityMeasure E} {Pâ‚€ : ProbabilityMeasure E} :
    Tendsto P atTop (ğ“ Pâ‚€) â†”
      âˆ€ a, Tendsto (fun n => (P n).map (f := fun x => âŸªx, aâŸ«) (by fun_prop))
        atTop (ğ“ (Pâ‚€.map (f := fun x => âŸªx, aâŸ«) (by fun_prop))) := by
  simp_rw [tendsto_iff_tendsto_charFun, toMeasure_map, charFun_map_inner_right]
  rw [forall_comm, â† Prod.forall',
    (show Surjective (fun p : â„ Ã— E => p.1 â€¢ p.2) by intro a; use (1, a); simp).forall]
