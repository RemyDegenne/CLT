/-
Copyright (c) 2024 Thomas Zhu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Zhu, RÃ©my Degenne
-/
import Mathlib.Analysis.Calculus.Taylor
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Clt.CharFun

/-!
The characteristic function is moment generating.

Still depends on: Peano form of Taylor's theorem (TODO: is there code for X)
-/

section Taylor

open Set
open scoped Topology

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

theorem taylor_isLittleO_univ {f : â„ â†’ E} {xâ‚€ : â„} {n : â„•} (hf : ContDiff â„ n f) :
    (fun x â†¦ f x - taylorWithinEval f n univ xâ‚€ x) =o[ğ“ xâ‚€] fun x â†¦ (x - xâ‚€) ^ n := by
  suffices (fun x â†¦ f x - taylorWithinEval f n univ xâ‚€ x) =o[ğ“[univ] xâ‚€] fun x â†¦ (x - xâ‚€) ^ n by
    simpa
  refine taylor_isLittleO (s := univ) convex_univ (mem_univ _) ?_
  simpa [contDiffOn_univ] using hf

end Taylor

open MeasureTheory ProbabilityTheory Complex
open scoped Nat Real NNReal ENNReal Topology RealInnerProductSpace

section ForMathlib

lemma integrable_norm_rpow_antitone {Î±} [MeasurableSpace Î±]
    (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    {E} [NormedAddCommGroup E] {f : Î± â†’ E} (hf : AEStronglyMeasurable f Î¼)
    {p q : â„} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) (hpq : p â‰¤ q)
    (hint : Integrable (fun x â†¦ â€–f xâ€– ^ q) Î¼) :
    Integrable (fun x â†¦ â€–f xâ€– ^ p) Î¼ := by
  rcases hp.eq_or_lt with (rfl | hp)
  Â· simp
  rcases hq.eq_or_lt with (rfl | hq)
  Â· exact (hp.not_ge hpq).elim
  revert hint
  convert fun h â†¦ MemLp.mono_exponent h (ENNReal.ofReal_le_ofReal hpq) using 1
  Â· rw [â† integrable_norm_rpow_iff hf, ENNReal.toReal_ofReal hq.le] <;> simp_all
  Â· rw [â† integrable_norm_rpow_iff hf, ENNReal.toReal_ofReal hp.le] <;> simp_all
  Â· infer_instance

lemma integrable_norm_pow_antitone {Î±} [MeasurableSpace Î±]
    (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    {E} [NormedAddCommGroup E] {f : Î± â†’ E} (hf : AEStronglyMeasurable f Î¼)
    {p q : â„•} (hpq : p â‰¤ q)
    (hint : Integrable (fun x â†¦ â€–f xâ€– ^ q) Î¼) :
    Integrable (fun x â†¦ â€–f xâ€– ^ p) Î¼ := by
  revert hint
  replace hpq : (p : â„) â‰¤ q := by simpa
  convert integrable_norm_rpow_antitone Î¼ hf
    p.cast_nonneg q.cast_nonneg hpq <;> rw [Real.rpow_natCast]

end ForMathlib

section InnerProductSpace

/-!
The `n`th derivative of `charFun Î¼`.
The proof uses results on iterated derivatives of the Fourier transform.
-/


variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
  [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]
  {Î¼ : Measure E} [IsProbabilityMeasure Î¼]

@[fun_prop]
theorem contDiff_charFun
    {n : â„•} (hint : Integrable (â€–Â·â€– ^ n) Î¼) :
    ContDiff â„ n (charFun Î¼) := by
  have hint' (k : â„•) (hk : k â‰¤ (n : â„•âˆ)) : Integrable (fun x â†¦ â€–xâ€– ^ k * â€–(1 : E â†’ â„‚) xâ€–) Î¼ := by
    simp only [Pi.one_apply, norm_one, mul_one]
    rw [Nat.cast_le] at hk
    exact integrable_norm_pow_antitone Î¼ aestronglyMeasurable_id hk hint
  simp_rw [funext charFun_eq_fourierIntegral']
  refine (VectorFourier.contDiff_fourierIntegral (L := innerSL â„) hint').comp ?_
  exact contDiff_const_smul _

@[fun_prop]
lemma continuous_charFun : Continuous (charFun Î¼) := by
  rw [â† contDiff_zero (ğ•œ := â„)]
  refine contDiff_charFun ?_
  suffices Integrable (fun _ â†¦ (1 : â„)) Î¼ by convert this
  fun_prop

end InnerProductSpace

variable {Î¼ : Measure â„} [IsProbabilityMeasure Î¼]

open VectorFourier in
theorem iteratedDeriv_charFun {n : â„•} {t : â„} (hint : Integrable (|Â·| ^ n) Î¼) :
    iteratedDeriv n (charFun Î¼) t = I ^ n * âˆ« x, x ^ n * exp (t * x * I) âˆ‚Î¼ := by
  have h : innerâ‚— â„ = (ContinuousLinearMap.mul â„ â„).toLinearMapâ‚â‚‚ := by ext; rfl
  have hint' (k : â„•) (hk : k â‰¤ (n : â„•âˆ)) : Integrable (fun x â†¦ â€–xâ€– ^ k * â€–(1 : â„ â†’ â„‚) xâ€–) Î¼ := by
    simp only [Pi.one_apply, norm_one, mul_one]
    rw [Nat.cast_le] at hk
    exact integrable_norm_pow_antitone Î¼ aestronglyMeasurable_id hk hint
  simp_rw [funext charFun_eq_fourierIntegral', smul_eq_mul]
  rw [iteratedDeriv_comp_const_smul]
  Â· dsimp only
    simp only [mul_inv_rev, neg_mul]
    rw [h, iteratedDeriv, iteratedFDeriv_fourierIntegral _ hint']
    Â· rw [fourierIntegral_continuousMultilinearMap_apply]
      Â· unfold fourierIntegral Real.fourierChar Circle.exp
        simp only [ContinuousMap.coe_mk, ofReal_mul, ofReal_ofNat,
          ContinuousLinearMap.toLinearMapâ‚â‚‚_apply, ContinuousLinearMap.mul_apply', mul_neg, neg_neg,
          AddChar.coe_mk, ofReal_inv, fourierPowSMulRight_apply, mul_one, Finset.prod_const,
          Finset.card_univ, Fintype.card_fin, Pi.one_apply, real_smul, ofReal_pow, smul_eq_mul,
          Circle.smul_def, ofReal_neg]
        simp_rw [mul_left_comm (exp _), integral_const_mul]
        calc (-((â†‘Ï€)â»Â¹ * 2â»Â¹)) ^ n
          * ((-(2 * â†‘Ï€ * I)) ^ n * âˆ« a, cexp (2 * â†‘Ï€ * (â†‘a * ((â†‘Ï€)â»Â¹ * 2â»Â¹ * â†‘t)) * I) * â†‘a ^ n âˆ‚Î¼)
        _ = I ^ n * âˆ« a, cexp (2 * â†‘Ï€ * (â†‘a * ((â†‘Ï€)â»Â¹ * 2â»Â¹ * â†‘t)) * I) * â†‘a ^ n âˆ‚Î¼ := by
          rw [â† mul_assoc]
          congr
          rw [â† mul_pow]
          ring_nf
          -- `âŠ¢ â†‘Ï€ ^ n * (â†‘Ï€)â»Â¹ ^ n * I ^ n = I ^ n`
          rw [inv_pow, mul_inv_cancelâ‚€, one_mul]
          norm_cast
          positivity
        _ = I ^ n * âˆ« x, â†‘x ^ n * cexp (â†‘t * â†‘x * I) âˆ‚Î¼ := by
          simp_rw [mul_comm ((_ : â„‚) ^ n)]
          congr with x
          congr 2
          ring_nf
          congr 2
          -- `âŠ¢ â†‘Ï€ * â†‘x * (â†‘Ï€)â»Â¹ = â†‘x`
          rw [mul_comm, â† mul_assoc, inv_mul_cancelâ‚€, one_mul]
          norm_cast
          positivity
      Â· exact Real.continuous_fourierChar
      Â· apply integrable_fourierPowSMulRight
        Â· simpa
        Â· exact aestronglyMeasurable_one
    Â· exact aestronglyMeasurable_one
    Â· rfl
  Â· rw [h]
    exact contDiff_fourierIntegral _ hint'

theorem iteratedDeriv_charFun_zero {n : â„•} (hint : Integrable (|Â·| ^ n) Î¼) :
    iteratedDeriv n (charFun Î¼) 0 = I ^ n * âˆ« x, x ^ n âˆ‚Î¼ := by
  simp only [iteratedDeriv_charFun hint, ofReal_zero, zero_mul, exp_zero, mul_one,
    mul_eq_mul_left_iff, pow_eq_zero_iff', I_ne_zero, ne_eq, false_and, or_false]
  norm_cast
  -- maybe this should have been done by norm_cast?
  exact integral_ofReal

lemma taylorWithinEval_charFun_zero {n : â„•} (hint : Integrable (|Â·| ^ n) Î¼) (t : â„):
    taylorWithinEval (charFun Î¼) n Set.univ 0 t
      = âˆ‘ k âˆˆ Finset.range (n + 1), (k ! : â„‚)â»Â¹ * (t * I) ^ k * âˆ« x, x ^ k âˆ‚Î¼ := by
  simp_rw [taylor_within_apply, sub_zero, RCLike.real_smul_eq_coe_mul]
  refine Finset.sum_congr rfl fun k hkn â†¦ ?_
  push_cast
  have hint' : Integrable (fun x â†¦ |x| ^ k) Î¼ :=
    integrable_norm_pow_antitone Î¼ aestronglyMeasurable_id (Finset.mem_range_succ_iff.mp hkn) hint
  rw [iteratedDerivWithin,
    iteratedFDerivWithin_eq_iteratedFDeriv uniqueDiffOn_univ _ (Set.mem_univ _),
    â† iteratedDeriv, iteratedDeriv_charFun_zero hint']
  Â· simp [mul_pow, mul_comm, mul_assoc, mul_left_comm]
  Â· exact (contDiff_charFun hint').contDiffAt

theorem taylor_charFun {n : â„•} (hint : Integrable (|Â·| ^ n) Î¼) :
    (fun t â†¦ charFun Î¼ t - âˆ‘ k âˆˆ Finset.range (n + 1), (k ! : â„)â»Â¹ * (t * I) ^ k * âˆ« x, x ^ k âˆ‚Î¼)
      =o[ğ“ 0] fun t â†¦ t ^ n := by
  have : (fun x â†¦ charFun Î¼ x - taylorWithinEval (charFun Î¼) n Set.univ 0 x)
      =o[ğ“ 0] fun x â†¦ x ^ n :=by
    convert taylor_isLittleO_univ (contDiff_charFun hint)
    simp_rw [sub_zero]
  convert this with t
  push_cast
  exact (taylorWithinEval_charFun_zero hint t).symm
