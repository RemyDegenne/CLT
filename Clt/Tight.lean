/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Clt.CharFun

/-!
# Tightness and characteristic functions

-/

open MeasureTheory ProbabilityTheory Filter
open scoped ENNReal NNReal Topology RealInnerProductSpace

lemma tendsto_iSup_of_tendsto_limsup {u : â„• â†’ â„ â†’ â„â‰¥0âˆ}
    (h_all : âˆ€ n, Tendsto (u n) atTop (ğ“ 0))
    (h_tendsto : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ u n r) atTop) atTop (ğ“ 0))
    (h_anti : âˆ€ n, Antitone (u n)) :
    Tendsto (fun r : â„ â†¦ â¨† n, u n r) atTop (ğ“ 0) := by
  simp_rw [ENNReal.tendsto_atTop_zero] at h_tendsto h_all âŠ¢
  intro Îµ hÎµ
  by_cases hÎµ_top : Îµ = âˆ
  Â· refine âŸ¨0, fun _ _ â†¦ by simp [hÎµ_top]âŸ©
  simp only [gt_iff_lt, ge_iff_le] at h_tendsto h_all hÎµ
  obtain âŸ¨r, hâŸ© := h_tendsto (Îµ / 2) (ENNReal.half_pos hÎµ.ne')
  have h' x (hx : r â‰¤ x) y (hy : Îµ / 2 < y) : âˆ€á¶  n in atTop, u n x < y := by
    specialize h x hx
    rw [limsup_le_iff] at h
    exact h y hy
  replace h' : âˆ€ x, r â‰¤ x â†’ âˆ€á¶  n in atTop, u n x < Îµ :=
    fun x hx â†¦ h' x hx Îµ (ENNReal.half_lt_self hÎµ.ne' hÎµ_top)
  simp only [eventually_atTop, ge_iff_le] at h'
  obtain âŸ¨N, hâŸ© := h' r le_rfl
  replace h_all : âˆ€ Îµ > 0, âˆ€ n, âˆƒ N, âˆ€ n_1 â‰¥ N, u n n_1 â‰¤ Îµ := fun Îµ hÎµ n â†¦ h_all n Îµ hÎµ
  choose rs hrs using h_all Îµ hÎµ
  refine âŸ¨r âŠ” â¨† n : Finset.range N, rs n, fun v hv â†¦ ?_âŸ©
  simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff, forall_apply_eq_imp_iff]
  intro n
  by_cases hn : n < N
  Â· refine hrs n v ?_
    calc rs n
    _ = rs (âŸ¨n, by simp [hn]âŸ© : Finset.range N) := rfl
    _ â‰¤ â¨† n : Finset.range N, rs n := by
      refine le_ciSup (f := fun (x : Finset.range N) â†¦ rs x) ?_ (âŸ¨n, by simp [hn]âŸ© : Finset.range N)
      exact Finite.bddAbove_range _
    _ â‰¤ r âŠ” â¨† n : Finset.range N, rs n := le_max_right _ _
    _ â‰¤ v := hv
  Â· have hn_le : N â‰¤ n := not_lt.mp hn
    specialize h n hn_le
    refine (h_anti n ?_).trans h.le
    calc r
    _ â‰¤ r âŠ” â¨† n : Finset.range N, rs n := le_max_left _ _
    _ â‰¤ v := hv

lemma iSup_set_seq {E : Type*} {_ : MeasurableSpace E} (Î¼ : â„• â†’ Measure E) {s : Set E} :
    â¨† Î¼' âˆˆ {Î¼ n | n}, Î¼' s = â¨† n, Î¼ n s := by
  apply le_antisymm
  Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff, forall_apply_eq_imp_iff]
    intro n
    exact le_iSup (fun i â†¦ Î¼ i s) n
  Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff]
    intro n
    calc Î¼ n s
    _ â‰¤ â¨† i, â¨† (_ : Î¼ i = Î¼ n), Î¼ i s := le_biSup (fun i â†¦ Î¼ i s) rfl
    _ = â¨† i, â¨† (_ : Î¼ i = Î¼ n), Î¼ n s := by
      convert rfl using 4 with m hm
      rw [hm]
    _ â‰¤ â¨† Î¼', â¨† i, â¨† (_ : Î¼ i = Î¼'), Î¼' s :=
      le_iSup (fun Î¼' â†¦ â¨† i, â¨† (_ : Î¼ i = Î¼'), Î¼' s) (Î¼ n)

lemma isTightMeasureSet_singleton {Î± : Type*} {mÎ± : MeasurableSpace Î±}
  [PseudoEMetricSpace Î±] [CompleteSpace Î±] [SecondCountableTopology Î±] [BorelSpace Î±]
  {Î¼ : Measure Î±} [IsFiniteMeasure Î¼] :
    IsTightMeasureSet {Î¼} :=
  isTightMeasureSet_singleton_of_innerRegularWRT
    (innerRegular_isCompact_isClosed_measurableSet_of_finite _)

variable {E : Type*} {mE : MeasurableSpace E} [NormedAddCommGroup E]

lemma norm_le_mul_iSup_abs_inner {Î¹ : Type*} [Fintype Î¹]
    [InnerProductSpace â„ E] [FiniteDimensional â„ E]
    (b : OrthonormalBasis Î¹ â„ E) (x : E) :
    â€–xâ€– â‰¤ âˆš(Fintype.card Î¹) * â¨† i, |âŸªb i, xâŸ«| := by
  rcases subsingleton_or_nontrivial E with hE | hE
  Â· have : x = 0 := Subsingleton.elim x 0
    simp [this]
  have h_rank : (0 : â„) < Fintype.card Î¹ := by
    simp only [â† Module.finrank_eq_card_basis b.toBasis, Nat.cast_pos, Module.finrank_pos_iff]
    infer_instance
  have : Nonempty Î¹ := by simpa [Fintype.card_pos_iff] using h_rank
  calc â€–xâ€–
  _ = âˆš(âˆ‘ i, âŸªb i, xâŸ« ^ 2) := by
    simp_rw [norm_eq_sqrt_real_inner, â† OrthonormalBasis.sum_inner_mul_inner b x x,
      real_inner_comm _ x, â† pow_two]
  _ = âˆš(âˆ‘ i, |âŸªb i, xâŸ«| ^ 2) := by simp
  _ â‰¤ âˆš(âˆ‘ _ : Î¹, (â¨† j, |âŸªb j, xâŸ«|) ^ 2) := by
    gcongr with i
    exact le_ciSup (f := fun j â†¦ |âŸªb j, xâŸ«|) (by simp) i
  _ = âˆš(Fintype.card Î¹) * â¨† i, |âŸªb i, xâŸ«| := by
    simp only [Finset.sum_const, Finset.card_univ, nsmul_eq_mul, Nat.cast_nonneg, Real.sqrt_mul]
    congr
    rw [Real.sqrt_sq]
    exact le_ciSup_of_le (by simp) (Nonempty.some this) (by positivity)

section FiniteDimensional

lemma isTightMeasureSet_of_tendsto_limsup_measure_norm_gt [BorelSpace E]
    [NormedSpace â„ E] [FiniteDimensional â„ E] {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)]
    (h : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < â€–xâ€–}) atTop) atTop (ğ“ 0)) :
    IsTightMeasureSet {Î¼ n | n} := by
  refine isTightMeasureSet_of_tendsto_measure_norm_gt ?_
  convert tendsto_iSup_of_tendsto_limsup (fun n â†¦ ?_) h fun n u v huv â†¦ ?_ with y
  Â· exact iSup_set_seq Î¼
  Â· have h_tight : IsTightMeasureSet {Î¼ n} :=
      isTightMeasureSet_singleton_of_innerRegularWRT
        (innerRegular_isCompact_isClosed_measurableSet_of_finite (Î¼ n))
    rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h_tight
    simpa using h_tight
  Â· refine measure_mono fun x hx â†¦ ?_
    simp only [Set.mem_setOf_eq] at hx âŠ¢
    exact huv.trans_lt hx

lemma isTightMeasureSet_iff_tendsto_limsup_measure_norm_gt [BorelSpace E]
    [NormedSpace â„ E] [FiniteDimensional â„ E] {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)] :
    IsTightMeasureSet {Î¼ n | n}
      â†” Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < â€–xâ€–}) atTop) atTop (ğ“ 0) := by
  refine âŸ¨fun h â†¦ ?_, isTightMeasureSet_of_tendsto_limsup_measure_norm_gtâŸ©
  have h_sup := tendsto_measure_norm_gt_of_isTightMeasureSet h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h_sup (fun _ â†¦ zero_le') ?_
  intro r
  simp_rw [iSup_set_seq]
  exact limsup_le_iSup

variable {Î¹ : Type*} [Fintype Î¹]

lemma isTightMeasureSet_of_forall_basis_tendsto [InnerProductSpace â„ E] [FiniteDimensional â„ E]
    {S : Set (Measure E)} (b : OrthonormalBasis Î¹ â„ E)
    (h : âˆ€ i, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < |âŸªb i, xâŸ«|}) atTop (ğ“ 0)) :
    IsTightMeasureSet S := by
  rcases subsingleton_or_nontrivial E with hE | hE
  Â· simp only [IsTightMeasureSet, cocompact_eq_bot, smallSets_bot]
    convert tendsto_pure_nhds (a := âˆ…) _
    simp
  have h_rank : (0 : â„) < Fintype.card Î¹ := by
    simp only [â† Module.finrank_eq_card_basis b.toBasis, Nat.cast_pos, Module.finrank_pos_iff]
    infer_instance
  have : Nonempty Î¹ := by simpa [Fintype.card_pos_iff] using h_rank
  refine isTightMeasureSet_of_tendsto_measure_norm_gt ?_
  have h_le : (fun r â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–})
      â‰¤ fun r â†¦ âˆ‘ i, â¨† Î¼ âˆˆ S, Î¼ {x | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|} := by
    intro r
    calc â¨† Î¼ âˆˆ S, Î¼ {x | r < â€–xâ€–}
    _ â‰¤ â¨† Î¼ âˆˆ S, Î¼ (â‹ƒ i, {x : E | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|}) := by
      gcongr with Î¼ hÎ¼S
      intro x hx
      simp only [Set.mem_setOf_eq, Set.mem_iUnion] at hx âŠ¢
      have hx' : r < âˆš(Fintype.card Î¹) * â¨† i, |âŸªb i, xâŸ«| :=
        hx.trans_le (norm_le_mul_iSup_abs_inner b x)
      rw [â† div_lt_iffâ‚€' (by positivity)] at hx'
      by_contra! h_le
      exact lt_irrefl (r / âˆš(Fintype.card Î¹)) (hx'.trans_le (ciSup_le h_le))
    _ â‰¤ â¨† Î¼ âˆˆ S, âˆ‘ i, Î¼ {x : E | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|} := by
      gcongr with Î¼ hÎ¼S
      exact measure_iUnion_fintype_le Î¼ _
    _ â‰¤ âˆ‘ i, â¨† Î¼ âˆˆ S, Î¼ {x | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|} := by
      refine iSup_le fun Î¼ â†¦ (iSup_le fun hÎ¼S â†¦ ?_)
      gcongr with i
      exact le_biSup (fun Î¼ â†¦ Î¼ {x | r / âˆš(Fintype.card Î¹) < |âŸªb i, xâŸ«|}) hÎ¼S
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds ?_ (fun _ â†¦ zero_le') h_le
  have : âˆ‘ i : Î¹, (0 : â„â‰¥0âˆ) = 0 := by simp
  rw [â† this]
  refine tendsto_finset_sum Finset.univ fun i _ â†¦ ?_
  refine (h i).comp ?_
  exact Tendsto.atTop_div_const (by positivity) tendsto_id

lemma isTightMeasureSet_iff_forall_basis_tendsto [InnerProductSpace â„ E] [FiniteDimensional â„ E]
    {S : Set (Measure E)} (b : OrthonormalBasis Î¹ â„ E) :
    IsTightMeasureSet S
      â†” âˆ€ i, Tendsto (fun r : â„ â†¦ â¨† Î¼ âˆˆ S, Î¼ {x | r < |âŸªb i, xâŸ«|}) atTop (ğ“ 0) := by
  refine âŸ¨fun h i â†¦ ?_, isTightMeasureSet_of_forall_basis_tendsto bâŸ©
  rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h (fun _ â†¦ zero_le') ?_
  intro r
  have h_le (Î¼ : Measure E) : Î¼ {x | r < |âŸªb i, xâŸ«|} â‰¤ Î¼ {x | r < â€–xâ€–} := by
    refine measure_mono fun x hx â†¦ ?_
    simp only [Set.mem_setOf_eq] at hx âŠ¢
    refine hx.trans_le ?_
    refine (abs_real_inner_le_norm _ _).trans ?_
    simp
  simp only [iSup_le_iff]
  intro Î¼ hÎ¼S
  refine le_iSup_of_le (i := Î¼) ?_
  simp only [hÎ¼S, iSup_pos]
  exact h_le Î¼

lemma isTightMeasureSet_of_forall_basis_tendsto_limsup [BorelSpace E]
    [InnerProductSpace â„ E] [FiniteDimensional â„ E] {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)]
    (b : OrthonormalBasis Î¹ â„ E)
    (h : âˆ€ i, Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªb i, xâŸ«|}) atTop) atTop (ğ“ 0)) :
    IsTightMeasureSet {Î¼ n | n} := by
  refine isTightMeasureSet_of_forall_basis_tendsto b fun i â†¦ ?_
  convert tendsto_iSup_of_tendsto_limsup (fun n â†¦ ?_) (h i) fun n u v huv â†¦ ?_ with y
  Â· apply le_antisymm
    Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff, forall_apply_eq_imp_iff]
      exact fun n â†¦ le_iSup (fun j â†¦ Î¼ j {x | y < |âŸªb i, xâŸ«|}) n
    Â· simp only [Set.mem_setOf_eq, iSup_exists, iSup_le_iff]
      intro n
      calc Î¼ n {x | y < |âŸªb i, xâŸ«|}
      _ â‰¤ â¨† j, â¨† (_ : Î¼ j = Î¼ n), Î¼ j {x | y < |âŸªb i, xâŸ«|} :=
          le_biSup (fun j â†¦ Î¼ j {x | y < |âŸªb i, xâŸ«|}) rfl
      _ = â¨† j, â¨† (_ : Î¼ j = Î¼ n), Î¼ n {x | y < |âŸªb i, xâŸ«|} := by
        convert rfl using 4 with m hm
        rw [hm]
      _ â‰¤ â¨† Î¼', â¨† j, â¨† (_ : Î¼ j = Î¼'), Î¼' {x | y < |âŸªb i, xâŸ«|} :=
        le_iSup (fun Î¼' â†¦ â¨† j, â¨† (_ : Î¼ j = Î¼'), Î¼' {x | y < |âŸªb i, xâŸ«|}) (Î¼ n)
  Â· have h_tight : IsTightMeasureSet {(Î¼ n).map (fun x â†¦ âŸªb i, xâŸ«)} :=
      isTightMeasureSet_singleton
    rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h_tight
    have h_map r : (Î¼ n).map (fun x â†¦ inner ((b) i) x) {x | r < |x|}
        = Î¼ n {x | r < |âŸªb i, xâŸ«|} := by
      rw [Measure.map_apply (by fun_prop)]
      Â· simp
      Â· exact MeasurableSet.preimage measurableSet_Ioi (by fun_prop)
    simpa [h_map] using h_tight
  Â· exact measure_mono fun x hx â†¦ huv.trans_lt hx

lemma isTightMeasureSet_iff_forall_basis_tendsto_limsup [BorelSpace E]
    [InnerProductSpace â„ E] [FiniteDimensional â„ E]
    {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)] (b : OrthonormalBasis Î¹ â„ E) :
    IsTightMeasureSet {Î¼ n | n}
      â†” âˆ€ i, Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªb i, xâŸ«|}) atTop) atTop (ğ“ 0) := by
  refine âŸ¨fun h i â†¦ ?_, isTightMeasureSet_of_forall_basis_tendsto_limsup bâŸ©
  rw [isTightMeasureSet_iff_forall_basis_tendsto b] at h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds (h i) (fun _ â†¦ zero_le') ?_
  intro r
  simp_rw [iSup_set_seq]
  exact limsup_le_iSup

/-- Let $(\mu_n)_{n \in \mathbb{N}}$ be measures on $\mathbb{R}^d$ with characteristic functions
$(\hat{\mu}_n)$. If $\hat{\mu}_n$ converges pointwise to a function $f$ which is continuous at 0,
then $(\mu_n)$ is tight. -/
lemma isTightMeasureSet_of_tendsto_charFun [InnerProductSpace â„ E] [FiniteDimensional â„ E]
    [BorelSpace E]
    {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsProbabilityMeasure (Î¼ i)]
    {f : E â†’ â„‚} (hf : ContinuousAt f 0) (hf_meas : Measurable f)
    (h : âˆ€ t, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (f t))) :
    IsTightMeasureSet {Î¼ i | i} := by
  refine isTightMeasureSet_of_forall_basis_tendsto_limsup (stdOrthonormalBasis â„ E) fun i â†¦ ?_
  have h_le n r := measure_abs_inner_ge_le_charFun (Î¼ := Î¼ n)
    (a := stdOrthonormalBasis â„ E i) (r := r)
  suffices Tendsto (fun (r : â„) â†¦
        limsup (fun n â†¦ (Î¼ n {x | r < |âŸªstdOrthonormalBasis â„ E i, xâŸ«|}).toReal) atTop)
      atTop (ğ“ 0) by
    have h_ofReal r : limsup (fun n â†¦ Î¼ n {x | r < |âŸªstdOrthonormalBasis â„ E i, xâŸ«|}) atTop
        = ENNReal.ofReal
          (limsup (fun n â†¦ (Î¼ n {x | r < |âŸªstdOrthonormalBasis â„ E i, xâŸ«|}).toReal) atTop) := by
      rw [ENNReal.limsup_toReal_eq (b := 1)]
      Â· rw [ENNReal.ofReal_toReal]
        refine ne_of_lt ?_
        calc limsup (fun n â†¦ (Î¼ n) {x | r < |âŸªstdOrthonormalBasis â„ E i, xâŸ«|}) atTop
        _ â‰¤ 1 := by
          refine limsup_le_of_le ?_ ?_
          Â· exact IsCoboundedUnder.of_frequently_ge <| .of_forall fun _ â†¦ zero_le'
          Â· exact .of_forall fun _ â†¦ prob_le_one
        _ < âŠ¤ := by simp
      Â· simp
      Â· exact .of_forall fun _ â†¦ prob_le_one
    simp_rw [h_ofReal]
    rw [â† ENNReal.ofReal_zero]
    exact ENNReal.tendsto_ofReal this
  have h_le_4 n r (hr : 0 < r) :
      2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹,
        1 - charFun (Î¼ n) (t â€¢ stdOrthonormalBasis â„ E i)â€– â‰¤ 4 := by
    have hr' : -(2 * râ»Â¹) â‰¤ 2 * râ»Â¹ := by rw [neg_le_self_iff]; positivity
    calc 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - charFun (Î¼ n) (t â€¢ stdOrthonormalBasis â„ E i)â€–
    _ â‰¤ 2â»Â¹ * r
        * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, â€–1 - charFun (Î¼ n) (t â€¢ stdOrthonormalBasis â„ E i)â€– := by
      simp only [neg_mul, intervalIntegrable_const]
      gcongr
      rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
      exact norm_integral_le_integral_norm _
    _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, 2 := by
      gcongr
      rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
      refine integral_mono_of_nonneg ?_ (by fun_prop) ?_
      Â· exact ae_of_all _ fun _ â†¦ by positivity
      Â· refine ae_of_all _ fun x â†¦ norm_one_sub_charFun_le_two
    _ â‰¤ 4 := by
      simp only [neg_mul, intervalIntegral.integral_const, sub_neg_eq_add, smul_eq_mul]
      ring_nf
      rw [mul_inv_cancelâ‚€ hr.ne', one_mul]
  -- We introduce an upper bound for the limsup.
  -- This is where we use the fact that `charFun (Î¼ n)` converges to `f`.
  have h_limsup_le r (hr : 0 < r) :
      limsup (fun n â†¦ (Î¼ n {x | r < |âŸªstdOrthonormalBasis â„ E i, xâŸ«|}).toReal) atTop
        â‰¤ 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ stdOrthonormalBasis â„ E i)â€– := by
    calc limsup (fun n â†¦ (Î¼ n {x | r < |âŸªstdOrthonormalBasis â„ E i, xâŸ«|}).toReal) atTop
    _ â‰¤ limsup (fun n â†¦ 2â»Â¹ * r
        * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - charFun (Î¼ n) (t â€¢ stdOrthonormalBasis â„ E i)â€–) atTop := by
      refine limsup_le_limsup (.of_forall fun n â†¦ h_le n r hr) ?_ ?_
      Â· exact IsCoboundedUnder.of_frequently_ge <| .of_forall fun _ â†¦ ENNReal.toReal_nonneg
      Â· refine âŸ¨4, ?_âŸ©
        simp only [eventually_map, eventually_atTop, ge_iff_le]
        exact âŸ¨0, fun n _ â†¦ h_le_4 n r hrâŸ©
    _ = 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ stdOrthonormalBasis â„ E i)â€– := by
      refine ((Tendsto.norm ?_).const_mul _).limsup_eq
      simp only [neg_mul, intervalIntegrable_const]
      have hr' : -(2 * râ»Â¹) â‰¤ 2 * râ»Â¹ := by rw [neg_le_self_iff]; positivity
      simp_rw [intervalIntegral.integral_of_le hr']
      refine tendsto_integral_of_dominated_convergence (fun _ â†¦ 2) ?_ (by fun_prop) ?_ ?_
      Â· exact fun _ â†¦ Measurable.aestronglyMeasurable <| by fun_prop
      Â· exact fun _ â†¦ ae_of_all _ fun _ â†¦ norm_one_sub_charFun_le_two
      Â· exact ae_of_all _ fun x â†¦ tendsto_const_nhds.sub (h _)
  -- It suffices to show that the upper bound tends to 0.
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds
    (h := fun r â†¦ 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ stdOrthonormalBasis â„ E i)â€–)
    ?_ ?_ ?_
  rotate_left
  Â· filter_upwards [eventually_gt_atTop 0] with r hr
    refine le_limsup_of_le ?_ fun u hu â†¦ ?_
    Â· refine âŸ¨4, ?_âŸ©
      simp only [eventually_map, eventually_atTop, ge_iff_le]
      exact âŸ¨0, fun n _ â†¦ (h_le n r hr).trans (h_le_4 n r hr)âŸ©
    Â· exact ENNReal.toReal_nonneg.trans hu.exists.choose_spec
  Â· filter_upwards [eventually_gt_atTop 0] with r hr using h_limsup_le r hr
  -- We now show that the upper bound tends to 0.
  -- This will follow from the fact that `f` is continuous at `0`.
  -- `âŠ¢ Tendsto (fun r â†¦ 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹,`
  --    `1 - f (t â€¢ stdOrthonormalBasis â„ E i)â€–) atTop (ğ“ 0)`
  have hf_tendsto := hf.tendsto
  rw [Metric.tendsto_nhds_nhds] at hf_tendsto
  rw [Metric.tendsto_atTop]
  intro Îµ hÎµ
  have hf0 : f 0 = 1 := by symm; simpa using h 0
  simp only [gt_iff_lt, dist_eq_norm_sub', zero_sub, norm_neg, hf0] at hf_tendsto
  simp only [ge_iff_le, neg_mul, intervalIntegrable_const, dist_zero_right, norm_mul, norm_inv,
    Real.norm_ofNat, Real.norm_eq_abs, norm_norm]
  simp_rw [abs_of_nonneg (norm_nonneg _)]
  obtain âŸ¨Î´, hÎ´, hÎ´_ltâŸ© : âˆƒ Î´, 0 < Î´ âˆ§ âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– < Î´ â†’ â€–1 - f xâ€– < Îµ / 4 :=
    hf_tendsto (Îµ / 4) (by positivity)
  refine âŸ¨4 * Î´â»Â¹, fun r hrÎ´ â†¦ ?_âŸ©
  have hr : 0 < r := lt_of_lt_of_le (by positivity) hrÎ´
  have hr' : -(2 * râ»Â¹) â‰¤ 2 * râ»Â¹ := by rw [neg_le_self_iff]; positivity
  have h_le_Ioc x (hx : x âˆˆ Set.Ioc (-(2 * râ»Â¹)) (2 * râ»Â¹)) :
      â€–1 - f (x â€¢ stdOrthonormalBasis â„ E i)â€– â‰¤ Îµ / 4 := by
    refine (hÎ´_lt ?_).le
    simp only [norm_smul, Real.norm_eq_abs, OrthonormalBasis.norm_eq_one, mul_one]
    calc |x|
    _ â‰¤ 2 * râ»Â¹ := by
      rw [abs_le]
      rw [Set.mem_Ioc] at hx
      exact âŸ¨hx.1.le, hx.2âŸ©
    _ < Î´ := by
      rw [â† lt_div_iffâ‚€' (by positivity), inv_lt_commâ‚€ hr (by positivity)]
      refine lt_of_lt_of_le ?_ hrÎ´
      ring_nf
      gcongr
      norm_num
  rw [abs_of_nonneg hr.le]
  calc 2â»Â¹ * r * â€–âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, 1 - f (t â€¢ stdOrthonormalBasis â„ E i)â€–
  _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, â€–1 - f (t â€¢ stdOrthonormalBasis â„ E i)â€– := by
    gcongr
    rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
    exact norm_integral_le_integral_norm _
  _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, Îµ / 4 := by
    gcongr
    rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
    refine integral_mono_ae ?_ (by fun_prop) ?_
    Â· refine Integrable.mono' (integrable_const (Îµ / 4)) ?_ ?_
      Â· exact Measurable.aestronglyMeasurable <| by fun_prop
      Â· simp_rw [norm_norm]
        exact ae_restrict_of_forall_mem measurableSet_Ioc h_le_Ioc
    Â· exact ae_restrict_of_forall_mem measurableSet_Ioc h_le_Ioc
  _ = Îµ / 2 := by
    simp only [intervalIntegral.integral_div, intervalIntegral.integral_const, sub_neg_eq_add,
      smul_eq_mul]
    ring_nf
    rw [mul_inv_cancelâ‚€ hr.ne', one_mul]
  _ < Îµ := by simp [hÎµ]

end FiniteDimensional

variable {Î¹ : Type*} [InnerProductSpace â„ E] {Î¼ : Î¹ â†’ Measure E} [âˆ€ i, IsProbabilityMeasure (Î¼ i)]

section EquicontinuousAt

lemma equicontinuousAt_charFun_zero_of_isTightMeasureSet (hÎ¼ : IsTightMeasureSet {Î¼ i | i}) :
    EquicontinuousAt (fun i â†¦ charFun (Î¼ i)) 0 := by
  sorry

lemma isTightMeasureSet_of_equicontinuousAt_charFun
    (hÎ¼ : EquicontinuousAt (fun i â†¦ charFun (Î¼ i)) 0) :
    IsTightMeasureSet {Î¼ i | i} := by
  sorry

lemma isTightMeasureSet_iff_equicontinuousAt_charFun :
    IsTightMeasureSet {Î¼ i | i} â†” EquicontinuousAt (fun i â†¦ charFun (Î¼ i)) 0 :=
  âŸ¨equicontinuousAt_charFun_zero_of_isTightMeasureSet,
    isTightMeasureSet_of_equicontinuousAt_charFunâŸ©

end EquicontinuousAt
