/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.IntegralCharFun
import Mathlib.MeasureTheory.Measure.TightNormed
import Clt.CharFun

/-!
# Tightness and characteristic functions

-/

open MeasureTheory ProbabilityTheory Filter
open scoped ENNReal NNReal Topology RealInnerProductSpace

lemma tendsto_iSup_of_tendsto_limsup {u : â„• â†’ â„ â†’ â„â‰¥0âˆ}
    (h_all : âˆ€ n, Tendsto (u n) atTop (ğ“ 0))
    (h_tendsto : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ u n r) atTop) atTop (ğ“ 0))
    (h_anti : âˆ€ n, Antitone (u n)) :
    Tendsto (fun r : â„ â†¦ â¨† n, u n r) atTop (ğ“ 0) := by
  simp_rw [ENNReal.tendsto_atTop_zero] at h_tendsto h_all âŠ¢
  intro Îµ hÎµ
  by_cases hÎµ_top : Îµ = âˆ
  Â· refine âŸ¨0, fun _ _ â†¦ by simp [hÎµ_top]âŸ©
  simp only [gt_iff_lt, ge_iff_le] at h_tendsto h_all hÎµ
  obtain âŸ¨r, hâŸ© := h_tendsto (Îµ / 2) (ENNReal.half_pos hÎµ.ne')
  have h' x (hx : r â‰¤ x) y (hy : Îµ / 2 < y) : âˆ€á¶  n in atTop, u n x < y := by
    specialize h x hx
    rw [limsup_le_iff] at h
    exact h y hy
  replace h' : âˆ€ x, r â‰¤ x â†’ âˆ€á¶  n in atTop, u n x < Îµ :=
    fun x hx â†¦ h' x hx Îµ (ENNReal.half_lt_self hÎµ.ne' hÎµ_top)
  simp only [eventually_atTop, ge_iff_le] at h'
  obtain âŸ¨N, hâŸ© := h' r le_rfl
  replace h_all : âˆ€ Îµ > 0, âˆ€ n, âˆƒ N, âˆ€ n_1 â‰¥ N, u n n_1 â‰¤ Îµ := fun Îµ hÎµ n â†¦ h_all n Îµ hÎµ
  choose rs hrs using h_all Îµ hÎµ
  refine âŸ¨r âŠ” â¨† n : Finset.range N, rs n, fun v hv â†¦ ?_âŸ©
  simp only [iSup_le_iff]
  intro n
  by_cases hn : n < N
  Â· refine hrs n v ?_
    calc rs n
    _ = rs (âŸ¨n, by simp [hn]âŸ© : Finset.range N) := rfl
    _ â‰¤ â¨† n : Finset.range N, rs n := by
      refine le_ciSup (f := fun (x : Finset.range N) â†¦ rs x) ?_ (âŸ¨n, by simp [hn]âŸ© : Finset.range N)
      exact Finite.bddAbove_range _
    _ â‰¤ r âŠ” â¨† n : Finset.range N, rs n := le_max_right _ _
    _ â‰¤ v := hv
  Â· have hn_le : N â‰¤ n := not_lt.mp hn
    specialize h n hn_le
    refine (h_anti n ?_).trans h.le
    calc r
    _ â‰¤ r âŠ” â¨† n : Finset.range N, rs n := le_max_left _ _
    _ â‰¤ v := hv

variable {E : Type*} {mE : MeasurableSpace E} [NormedAddCommGroup E]

section FiniteDimensional

variable [BorelSpace E] {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsFiniteMeasure (Î¼ i)]

section NormedSpace

variable [NormedSpace â„ E] [FiniteDimensional â„ E]

lemma isTightMeasureSet_of_tendsto_limsup_measure_norm_gt
    (h : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < â€–xâ€–}) atTop) atTop (ğ“ 0)) :
    IsTightMeasureSet (Set.range Î¼) := by
  refine isTightMeasureSet_of_tendsto_measure_norm_gt ?_
  simp_rw [iSup_range]
  refine tendsto_iSup_of_tendsto_limsup (fun n â†¦ ?_) h fun n u v huv â†¦ ?_
  Â· have h_tight : IsTightMeasureSet {Î¼ n} := isTightMeasureSet_singleton
    rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h_tight
    simpa using h_tight
  Â· refine measure_mono fun x hx â†¦ ?_
    simp only [Set.mem_setOf_eq] at hx âŠ¢
    exact huv.trans_lt hx

lemma isTightMeasureSet_iff_tendsto_limsup_measure_norm_gt :
    IsTightMeasureSet (Set.range Î¼)
      â†” Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < â€–xâ€–}) atTop) atTop (ğ“ 0) := by
  refine âŸ¨fun h â†¦ ?_, isTightMeasureSet_of_tendsto_limsup_measure_norm_gtâŸ©
  have h_sup := tendsto_measure_norm_gt_of_isTightMeasureSet h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds h_sup (fun _ â†¦ zero_le') ?_
  intro r
  simp_rw [iSup_range]
  exact limsup_le_iSup

end NormedSpace

section InnerProductSpace

variable [InnerProductSpace â„ E] [FiniteDimensional â„ E]

lemma isTightMeasureSet_of_tendsto_limsup_inner
    (h : âˆ€ z, Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªz, xâŸ«|}) atTop) atTop (ğ“ 0)) :
    IsTightMeasureSet (Set.range Î¼) := by
  refine isTightMeasureSet_of_inner_tendsto (ğ•œ := â„) fun z â†¦ ?_
  simp_rw [iSup_range]
  refine tendsto_iSup_of_tendsto_limsup (fun n â†¦ ?_) (h z) fun n u v huv â†¦ ?_
  Â· have h_tight : IsTightMeasureSet {(Î¼ n).map (fun x â†¦ âŸªz, xâŸ«)} := isTightMeasureSet_singleton
    rw [isTightMeasureSet_iff_tendsto_measure_norm_gt] at h_tight
    have h_map r : (Î¼ n).map (fun x â†¦ âŸªz, xâŸ«) {x | r < |x|} = Î¼ n {x | r < |âŸªz, xâŸ«|} := by
      rw [Measure.map_apply (by fun_prop)]
      Â· simp
      Â· exact MeasurableSet.preimage measurableSet_Ioi (by fun_prop)
    simpa [h_map] using h_tight
  Â· exact measure_mono fun x hx â†¦ huv.trans_lt hx

lemma isTightMeasureSet_iff_tendsto_limsup_inner :
    IsTightMeasureSet (Set.range Î¼)
      â†” âˆ€ z, Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªz, xâŸ«|}) atTop) atTop (ğ“ 0) := by
  refine âŸ¨fun h z â†¦ ?_, isTightMeasureSet_of_tendsto_limsup_innerâŸ©
  rw [isTightMeasureSet_iff_inner_tendsto â„] at h
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds (h z)
    (fun _ â†¦ zero_le') fun r â†¦ ?_
  simp_rw [iSup_range]
  exact limsup_le_iSup

lemma isTightMeasureSet_of_tendsto_limsup_inner_of_norm_eq_one
    (h : âˆ€ z, â€–zâ€– = 1
      â†’ Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | r < |âŸªz, xâŸ«|}) atTop) atTop (ğ“ 0)) :
    IsTightMeasureSet (Set.range Î¼) := by
  have : ProperSpace E := FiniteDimensional.proper â„ E
  refine isTightMeasureSet_of_tendsto_limsup_inner fun y â†¦ ?_
  by_cases hy : y = 0
  Â· simp only [hy, inner_zero_left, abs_zero]
    refine (tendsto_congr' ?_).mpr tendsto_const_nhds
    filter_upwards [eventually_ge_atTop 0] with r hr
    simp [not_lt.mpr hr]
  have h' : Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ Î¼ n {x | â€–yâ€–â»Â¹ * r < |âŸªâ€–yâ€–â»Â¹ â€¢ y, xâŸ«|}) atTop)
      atTop (ğ“ 0) := by
    specialize h (â€–yâ€–â»Â¹ â€¢ y) ?_
    Â· simp only [norm_smul, norm_inv, norm_norm]
      rw [inv_mul_cancelâ‚€ (by positivity)]
    exact h.comp <| (tendsto_const_mul_atTop_of_pos (by positivity)).mpr tendsto_id
  convert h' using 7 with r n x
  rw [inner_smul_left]
  simp only [map_invâ‚€, conj_trivial, abs_mul, abs_inv, abs_norm]
  rw [mul_lt_mul_iff_rightâ‚€]
  positivity

/-- If the characteristic functions converge pointwise to a function which is continuous at 0,
then `{Î¼ n | n}` is tight. -/
lemma isTightMeasureSet_of_tendsto_charFun {Î¼ : â„• â†’ Measure E} [âˆ€ i, IsProbabilityMeasure (Î¼ i)]
    {f : E â†’ â„‚} (hf : ContinuousAt f 0) (hf_meas : Measurable f)
    (h : âˆ€ t, Tendsto (fun n â†¦ charFun (Î¼ n) t) atTop (ğ“ (f t))) :
    IsTightMeasureSet (Set.range Î¼) := by
  refine isTightMeasureSet_of_tendsto_limsup_inner_of_norm_eq_one fun z hz â†¦ ?_
  suffices Tendsto (fun r : â„ â†¦ limsup (fun n â†¦ (Î¼ n).real {x | r < |âŸªz, xâŸ«|}) atTop) atTop (ğ“ 0) by
    have h_ofReal r : limsup (fun n â†¦ Î¼ n {x | r < |âŸªz, xâŸ«|}) atTop
        = ENNReal.ofReal (limsup (fun n â†¦ (Î¼ n).real {x | r < |âŸªz, xâŸ«|}) atTop) := by
      simp_rw [measureReal_def,
        ENNReal.limsup_toReal_eq (b := 1) (by simp) (.of_forall fun _ â†¦ prob_le_one)]
      rw [ENNReal.ofReal_toReal]
      refine ne_top_of_le_ne_top (by simp : 1 â‰  âˆ) ?_
      refine limsup_le_of_le ?_ (.of_forall fun _ â†¦ prob_le_one)
      exact IsCoboundedUnder.of_frequently_ge <| .of_forall fun _ â†¦ zero_le'
    simp_rw [h_ofReal]
    rw [â† ENNReal.ofReal_zero]
    exact ENNReal.tendsto_ofReal this
  have h_le_4 n r (hr : 0 < r) :
      2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - charFun (Î¼ n) (t â€¢ z)â€– â‰¤ 4 := by
    have hr' : -(2 * râ»Â¹) â‰¤ 2 * râ»Â¹ := by rw [neg_le_self_iff]; positivity
    calc 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - charFun (Î¼ n) (t â€¢ z)â€–
    _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, â€–1 - charFun (Î¼ n) (t â€¢ z)â€– := by
      simp only [neg_mul]
      gcongr
      rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
      exact norm_integral_le_integral_norm _
    _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, 2 := by
      gcongr
      rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
      refine integral_mono_of_nonneg ?_ (by fun_prop) ?_
      Â· exact ae_of_all _ fun _ â†¦ by positivity
      Â· refine ae_of_all _ fun x â†¦ norm_one_sub_charFun_le_two
    _ â‰¤ 4 := by
      simp only [intervalIntegral.integral_const, sub_neg_eq_add, smul_eq_mul]
      ring_nf
      rw [mul_inv_cancelâ‚€ hr.ne', one_mul]
  have h_le n r := measureReal_abs_inner_gt_le_integral_charFun (Î¼ := Î¼ n) (a := z) (r := r)
  -- We introduce an upper bound for the limsup.
  -- This is where we use the fact that `charFun (Î¼ n)` converges to `f`.
  have h_limsup_le r (hr : 0 < r) :
      limsup (fun n â†¦ (Î¼ n).real {x | r < |âŸªz, xâŸ«|}) atTop
        â‰¤ 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ z)â€– := by
    calc limsup (fun n â†¦ (Î¼ n).real {x | r < |âŸªz, xâŸ«|}) atTop
    _ â‰¤ limsup (fun n â†¦ 2â»Â¹ * r
        * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - charFun (Î¼ n) (t â€¢ z)â€–) atTop := by
      refine limsup_le_limsup (.of_forall fun n â†¦ h_le n r hr) ?_ ?_
      Â· exact IsCoboundedUnder.of_frequently_ge <| .of_forall fun _ â†¦ ENNReal.toReal_nonneg
      Â· refine âŸ¨4, ?_âŸ©
        simp only [eventually_map, eventually_atTop, ge_iff_le]
        exact âŸ¨0, fun n _ â†¦ h_le_4 n r hrâŸ©
    _ = 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ z)â€– := by
      refine ((Tendsto.norm ?_).const_mul _).limsup_eq
      simp only [neg_mul]
      have hr' : -(2 * râ»Â¹) â‰¤ 2 * râ»Â¹ := by rw [neg_le_self_iff]; positivity
      simp_rw [intervalIntegral.integral_of_le hr']
      refine tendsto_integral_of_dominated_convergence (fun _ â†¦ 2) ?_ (by fun_prop) ?_ ?_
      Â· exact fun _ â†¦ Measurable.aestronglyMeasurable <| by fun_prop
      Â· exact fun _ â†¦ ae_of_all _ fun _ â†¦ norm_one_sub_charFun_le_two
      Â· exact ae_of_all _ fun x â†¦ tendsto_const_nhds.sub (h _)
  -- It suffices to show that the upper bound tends to 0.
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds
    (h := fun r â†¦ 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ z)â€–) ?_ ?_ ?_
  rotate_left
  Â· filter_upwards [eventually_gt_atTop 0] with r hr
    refine le_limsup_of_le ?_ fun u hu â†¦ ?_
    Â· refine âŸ¨4, ?_âŸ©
      simp only [eventually_map, eventually_atTop, ge_iff_le]
      exact âŸ¨0, fun n _ â†¦ (h_le n r hr).trans (h_le_4 n r hr)âŸ©
    Â· exact ENNReal.toReal_nonneg.trans hu.exists.choose_spec
  Â· filter_upwards [eventually_gt_atTop 0] with r hr using h_limsup_le r hr
  -- We now show that the upper bound tends to 0.
  -- This will follow from the fact that `f` is continuous at `0`.
  -- `âŠ¢ Tendsto (fun r â†¦ 2â»Â¹ * r * â€–âˆ« t in -2 * râ»Â¹..2 * râ»Â¹, 1 - f (t â€¢ z)â€–) atTop (ğ“ 0)`
  have hf_tendsto := hf.tendsto
  rw [Metric.tendsto_nhds_nhds] at hf_tendsto
  rw [Metric.tendsto_atTop]
  intro Îµ hÎµ
  have hf0 : f 0 = 1 := by symm; simpa using h 0
  simp only [gt_iff_lt, dist_eq_norm_sub', zero_sub, norm_neg, hf0] at hf_tendsto
  simp only [ge_iff_le, neg_mul, dist_zero_right, norm_mul, norm_inv,
    Real.norm_ofNat, Real.norm_eq_abs]
  simp_rw [abs_of_nonneg (norm_nonneg _)]
  obtain âŸ¨Î´, hÎ´, hÎ´_ltâŸ© : âˆƒ Î´, 0 < Î´ âˆ§ âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– < Î´ â†’ â€–1 - f xâ€– < Îµ / 4 :=
    hf_tendsto (Îµ / 4) (by positivity)
  refine âŸ¨4 * Î´â»Â¹, fun r hrÎ´ â†¦ ?_âŸ©
  have hr : 0 < r := lt_of_lt_of_le (by positivity) hrÎ´
  have hr' : -(2 * râ»Â¹) â‰¤ 2 * râ»Â¹ := by rw [neg_le_self_iff]; positivity
  have h_le_Ioc x (hx : x âˆˆ Set.Ioc (-(2 * râ»Â¹)) (2 * râ»Â¹)) :
      â€–1 - f (x â€¢ z)â€– â‰¤ Îµ / 4 := by
    refine (hÎ´_lt ?_).le
    simp only [norm_smul, Real.norm_eq_abs, mul_one, hz]
    calc |x|
    _ â‰¤ 2 * râ»Â¹ := by
      rw [abs_le]
      rw [Set.mem_Ioc] at hx
      exact âŸ¨hx.1.le, hx.2âŸ©
    _ < Î´ := by
      rw [â† lt_div_iffâ‚€' (by positivity), inv_lt_commâ‚€ hr (by positivity)]
      refine lt_of_lt_of_le ?_ hrÎ´
      ring_nf
      gcongr
      norm_num
  rw [abs_of_nonneg hr.le]
  calc 2â»Â¹ * r * â€–âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, 1 - f (t â€¢ z)â€–
  _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, â€–1 - f (t â€¢ z)â€– := by
    gcongr
    rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
    exact norm_integral_le_integral_norm _
  _ â‰¤ 2â»Â¹ * r * âˆ« t in -(2 * râ»Â¹)..2 * râ»Â¹, Îµ / 4 := by
    gcongr
    rw [intervalIntegral.integral_of_le hr', intervalIntegral.integral_of_le hr']
    refine integral_mono_ae ?_ (by fun_prop) ?_
    Â· refine Integrable.mono' (integrable_const (Îµ / 4)) ?_ ?_
      Â· exact Measurable.aestronglyMeasurable <| by fun_prop
      Â· simp_rw [norm_norm]
        exact ae_restrict_of_forall_mem measurableSet_Ioc h_le_Ioc
    Â· exact ae_restrict_of_forall_mem measurableSet_Ioc h_le_Ioc
  _ = Îµ / 2 := by
    simp only [intervalIntegral.integral_div, intervalIntegral.integral_const, sub_neg_eq_add,
      smul_eq_mul]
    ring_nf
    rw [mul_inv_cancelâ‚€ hr.ne', one_mul]
  _ < Îµ := by simp [hÎµ]

end InnerProductSpace

end FiniteDimensional
